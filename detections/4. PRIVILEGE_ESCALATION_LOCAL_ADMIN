# Privilege Escalation via Local Administrator Group

## Description
Detects the addition of user accounts to the local Administrators group,
a common privilege escalation and persistence technique used by attackers.
The detection matches member SIDs to usernames using correlated account
creation events.

## Data Sources
- Windows Security Logs
- Event ID 4720 — User Account Created
- Event ID 4732 — Security Group Membership Change
- Splunk sourcetype: WinEventLog:Security

## Detection Logic (Splunk SPL)

```spl
index=main sourcetype="WinEventLog:Security" EventCode=4732 earliest=-24h
| rex field=_raw "(?ms)Subject:.*?Account Name:\s+(?<Actor>[^\r\n]+)"
| rex field=_raw "(?ms)Member:.*?Security ID:\s+(?<MemberSID>[^\r\n]+)"
| rex field=_raw "(?ms)Group:.*?Group Name:\s+(?<GroupName>[^\r\n]+)"
| search GroupName="Administrators"
| join type=left MemberSID [
    search index=main sourcetype="WinEventLog:Security" EventCode=4720 earliest=-24h
    | rex field=_raw "(?ms)New Account:.*?Security ID:\s+(?<MemberSID>[^\r\n]+)"
    | rex field=_raw "(?ms)New Account:.*?Account Name:\s+(?<MemberAdded>[^\r\n]+)"
    | stats latest(MemberAdded) as MemberAdded by MemberSID
]
| eval MemberAdded=coalesce(MemberAdded,"UNKNOWN_SID")
| table _time host Actor MemberAdded MemberSID GroupName

## Alert Configuration
Type: Scheduled
Frequency: Every 1 hour
Time Window: Last 60 minutes
Trigger: Per result
Throttle: 60 minutes

## MITRE ATT&CK Mappin
T1098 — Account Manipulation
T1078 — Valid Accounts

## SOC Response Steps
-Validate legitimacy of the added account
-Identify the actor who performed the change
-Disable or remove unauthorized admin accounts
-Review recent authentication and activity
-Reset credentials if compromise is suspected

## Detection Design Notes

Windows Event ID 4732 may record group members as SIDs with the Account Name
field set to "-". This detection extracts the Member SID and enriches it
to a username by correlating with Event ID 4720.

Screenshots:
Splunk-4720.png
Splunk-4732.png
splunk-4720-user-created.png
splunk-4732-enriched-member.png

